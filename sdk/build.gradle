apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'org.jetbrains.dokka-android'

apply plugin: 'digital.wup.android-maven-publish'

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 17 // This is strictly the minimum one can support while having somewhat secure javascript
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.pro'
    }
    testOptions {
        unitTests.includeAndroidResources = true
    }
    dokka {
        externalDocumentationLink {
            url = new URL("https://developer.android.com/reference/")
            packageListUrl = new URL("https://developer.android.com/reference/androidx/package-list")
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2'

    // appcompat 1.1.0 has a bug which causes crashes with WebView
    // https://issuetracker.google.com/issues/141132133
    implementation 'androidx.appcompat:appcompat:1.1.0-rc01'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'

    implementation 'com.google.android.gms:play-services-base:17.1.0'
    implementation 'com.google.android.gms:play-services-basement:17.1.1'

    // okhttp 3.12.x is the last version to support minSdkVersion < 21.
    // Supporting minSdkVersion 17 is not necessarily wise anymore,
    // but we try our best anyway. Library user is free to have
    // a higher minSdkVersion and thus use a newer okhttp.
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:3.12.6'
    implementation 'com.google.code.gson:gson:2.8.5'

    api 'joda-time:joda-time:2.10.5'
    api 'org.threeten:threetenbp:1.4.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation 'org.robolectric:robolectric:4.3'
    testImplementation 'org.mockito:mockito-core:3.1.0'
    testImplementation 'org.mockito:mockito-inline:3.1.0'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation "com.squareup.okhttp3:mockwebserver:3.12.6"
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

publishing {
    publications {
        mavenAar(MavenPublication) {
            from components.android
            artifact(sourceJar)
            groupId 'com.swedbankpay.mobilesdk'
            artifactId 'mobilesdk'
            version '1.0.0-beta14'
        }
    }
}
